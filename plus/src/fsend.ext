EXTENTION fsend-1.0-1

namespace eval fsend {
    variable dcc

    proc init {} {
	onevent usercmd FSEND [namespace current]::ucmd_fsend \
			FGET [namespace current]::ucmd_fget \
			FCLOSE [namespace current]::ucmd_fclose \
			FLIST [namespace current]::ucmd_flist
	onevent servercmd FSEND [namespace current]::scmd_fsend
	register_msgs {
	    xcplus_fsend_error_nonick "*** Error: no nick name specified."
	    xcplus_fsend_error_nofile "*** Error: no file name specified."
	    xcplus_fsend_error_openfile "*** Error: couldn't open '%f'."
	    xcplus_fsend_error_init "*** Error: Failed initilaizing file transfer."
	    xcplus_fsend_error_magickey "*** Error: Incoming magickey from %s is WORNG."
	    xcplus_fsend_error_nosuch "*** Error: No such file transfer exists."
	    xcplus_fsend_established "*** File transfer with %n of file '%f' established."
	    xcplus_fsend_completed "*** File transfer with %n of file '%f' completed."
	    xcplus_fsend_incoming "*** Incoming file transfer '%f' from %n. Write '/fget %x' to receive it."
	    xcplus_fsend_bigger "*** Warning: incoming file is larger than %n said. closing connection."
	    xcplus_fsend_inform "*** Sending file '%f' to %n."
	    xcplus_fsend_closed "*** Closed file transfer with %n of file '%f'."
	    xcplus_fsend_list_header "Transfer list:"
	    xcplus_fsend_list_send "%x. Waiting for %n to accept '%f'"
	    xcplus_fsend_list_send_e "%x. Sending '%f' to %n"
	    xcplus_fsend_list_get "%x. Incoming transfer of '%f' from %n"
	    xcplus_fsend_list_get_e "%x. Getting file '%f' from %n"
	}
    }
    
    proc destroy {} {
	unevent usercmd [list [namespace current]::ucmd_fsend \
			      [namespace current]::ucmd_fget \
			      [namespace current]::ucmd_flist \
			      [namespace current]::ucmd_fclose]
	unevent servercmd [namespace current]::scmd_fsend
	namespace delete [namespace current]
    }
    
    proc strip_file {file} {
	set first [string last {/} $file]
	if {$first != -1} {
	    set file [string range $file [expr $first + 1] end]
	}
	return $file
    }
    
    proc scmd_fsend {nick uargs} {
	variable dcc
	set uargs [split $uargs " "]
	set file [strip_file [lindex $uargs 0]]
	set num [allocate_dcc GET $nick $file]
	putcmsg xcplus_fsend_incoming n $nick f $file x $num
	set dcc($num,filesize) [lindex $uargs 1]
	set dcc($num,nowsize) 0
	set dcc($num,ip) [lindex $uargs 2]
	set dcc($num,port) [lindex $uargs 3]
	set dcc($num,magickey) [lindex $uargs 4]
    }
    
    proc ucmd_flist {uargs} {
	variable dcc
	putcmsg xcplus_fsend_list_header
	foreach var [array names dcc *,type] {
	    set num [lindex [split $var ,] 0]
	    if {$dcc($var) == "SEND"} {
		set type xcplus_fsend_list_send
	    } else {
		set type xcplus_fsend_list_get
	    }
	    if {$dcc($num,working)} {
		set type ${type}_e
	    }
	    putcmsg $type x $num n $dcc($num,nick) f $dcc($num,file)
	}
	return 1
    }
    
    proc ucmd_fsend {uargs} {
	set nick [strtok uargs]
	set file [strtok uargs]
	if {$nick == ""} {
	    putcmsg xcplus_fsend_error_nonick 
	    return 1
	}
	if {$file == ""} {
	    set file [tk_getOpenFile]
	    if {$file == ""} {
		putcmsg xcplus_fsend_error_nofile
		return 1
	    }
	} 
	if {![file isfile $file]} {
	    putcmsg xcplus_fsend_error_openfile f $file
	    return 1
	}
	dcc_send $nick $file
	return 1
    }
    
    proc ucmd_fget {uargs} {
	variable dcc
	set arg [strtok uargs]
	if [catch {expr $arg + 1}] {
	    set num [dcc_search $arg]
	} else {
	    set num $arg
	}
	if {[array names dcc $num,type] == ""} {
	    putcmsg xcplus_fsend_error_nosuch
	    return 1
	}
	if {$dcc($num,working) == 1} {
	    return 1
	}
	if {$dcc($num,type) != "GET"} {
	    putcmsg xcplus_fsend_error_worngtype
	    return 1	
	}
	set file [tk_getSaveFile -initialfile $dcc($num,file) -initialdir .]
	if {$file == ""} {
	    putcmsg xcplus_fsend_error_nofile
	    return 1
	}
	if ![dcc_get $num $file] {
	    free_dcc $num
	}
	return 1
    }
        
    proc ucmd_fclose {uargs} {
	variable dcc
	set arg [strtok uargs]
	if [catch {expr $arg + 1}] {
	    set num [dcc_search $arg]
	} else {
	    set num $arg
	}
	if {[array names dcc $num,type] == ""} {
	    putcmsg xcplus_fsend_error_nosuch
	    return 1
	}
        putcmsg xcplus_fsend_closed n $dcc($num,nick) f $dcc($num,file)
	free_dcc $num
	return 1
    }
    
    proc dcc_search {nick} {
	variable dcc
	foreach var [array names dcc *,nick] {
	    if {$nick == $dcc($var)} {
		return [lindex [split $var ,] 0]
	    }
	}
	return -1
    }
    
    proc dcc_send {nick file} {
	variable dcc
	if [catch {open $file r} fd] {
	    putcmsg xcplus_fsend_error_init
	    return
	}
	set num [allocate_dcc SEND $nick [strip_file $file] $fd]
	set dcc($num,realfile) $file
	if {![dcc_listen $num]} {
	    free_dcc $num
	    putcmsg xcplus_fsend_error_init
	    return
	}
	dcc_informuser $num [file size $file]
    }
    
    proc dcc_get {num file} {
	variable dcc
	if {$file == ""} {
	    set file $dcc($num,file)
	}
	if [catch {open $file w} dcc($num,fd)] {
	    unset dcc($num,fd)
	    putcmsg xcplus_fsend_error_init
	    free_dcc $num
	    return 0
	} 
	if [catch {socket $dcc($num,ip) $dcc($num,port)} dcc($num,myfd)] {
	    unset dcc($num,myfd)
	    putcmsg xcplus_fsend_error_init
	    free_dcc $num
	    return 0
	}
	fconfigure $dcc($num,myfd) -blocking 0 -translation auto -buffering line
	puts $dcc($num,myfd) $dcc($num,magickey)
	unset dcc($num,magickey)
	flush $dcc($num,myfd)
	set dcc($num,working) 1
	fconfigure $dcc($num,myfd) -blocking 0 -translation binary -buffering none
	putcmsg xcplus_fsend_established n $dcc($num,nick) f $dcc($num,file)
	fcopy $dcc($num,myfd) $dcc($num,fd) -command [list [namespace current]::dcc_client_in $num $dcc($num,myfd)] -size 1024
	return 1
    }
    
    proc free_dcc {num} {
	variable dcc
	foreach var [array names dcc "$num,*fd"] {
	    if {$dcc($var) >= 0} { close $dcc($var) }
	}
	foreach var [array names dcc $num,*] {unset dcc($var)}
    }
    
    proc allocate_dcc {type nick file {fd -1}} {
	variable dcc
	set x 1
	foreach var [array names dcc {*,type}] {incr x}
	set dcc($x,type) $type
	set dcc($x,nick) $nick
	set dcc($x,file) $file
	set dcc($x,working) 0
	if {$fd != -1} { set dcc($x,fd) $fd }
	return $x
    }
    
    proc dcc_listen {num} {
	variable dcc
	set dcc($num,magickey) "xc[clock clicks][clock seconds][clock clicks][expr rand()]"
	if [catch {socket -server "[namespace current]::dcc_newconn $num" 0} dcc($num,listenfd)] {
	    unset dcc($num,listenfd)
	    return 0
	}
	set dcc($num,port) [lindex [fconfigure $dcc($num,listenfd) -sockname] 2]
	return 1
    }
    
    proc dcc_informuser {num size} {
	variable dcc
	global sock
	set ip [lindex [fconfigure $sock -sockname] 0]
	putcmsg xcplus_fsend_inform f $dcc($num,file) n $dcc($num,nick)
	putsock "CMD $dcc($num,nick) FSEND $dcc($num,file) $size $ip $dcc($num,port) $dcc($num,magickey)"
    }
    
    proc dcc_newconn {num idx host port} {
	variable dcc
	if {[array names dcc $num,type] == ""} {
	    close $idx
	    return
	}
	close $dcc($num,listenfd)
	unset dcc($num,listenfd)
	set dcc($num,clientfd) $idx
	fconfigure $idx -blocking 0 -translation auto -buffering line
	fileevent $idx readable "[namespace current]::dcc_server_in $num $idx"
	putcmsg xcplus_fsend_established n $dcc($num,nick) f $dcc($num,file)
    }
    
    proc dcc_server_in {num idx} {
	variable dcc
	if {[array names dcc $num,type] == ""} {
	    close $idx
	    return
	}
    	if [catch {set buf [gets $idx]}] {return}
	if {$buf == ""} {return}
	if {$buf != $dcc($num,magickey)} {
	    putcmsg xcplus_fsend_error_magickey s [lindex [fconfigure $idx -peername] 0]
	    free_dcc $num
	    return
	}
	unset dcc($num,magickey)
	set dcc($num,working) 1
	fconfigure $idx -blocking 0 -translation binary -buffering none
	fileevent $idx readable ""
	fcopy $dcc($num,fd) $idx -command [list [namespace current]::dcc_server_out $num $idx] -size 1024
    }
    
    proc dcc_server_out {num idx bytes {error {}}} {
	variable dcc
	flush $idx
	if {[string length $error] != 0 || [eof $dcc($num,fd)]} {
	    putcmsg xcplus_fsend_completed f $dcc($num,file) n $dcc($num,nick)
	    free_dcc $num
	    return 1
	} else {
	    fcopy $dcc($num,fd) $idx -command [list [namespace current]::dcc_server_out $num $idx] -size 1024
	}
    }
    
    proc dcc_client_in {num idx bytes {error {}}} {
	variable dcc
	flush $dcc($num,fd)
	incr dcc($num,nowsize) $bytes
	if {[string length $error] != 0 || [eof $idx] || $dcc($num,nowsize) == $dcc($num,filesize)} {
	    putcmsg xcplus_fsend_completed f $dcc($num,file) n $dcc($num,nick)
	    free_dcc $num
	} elseif {$dcc($num,nowsize) > $dcc($num,filesize)} {
	    putcmsg xcplus_fsend_bigger n $dcc($num,nick)
	    free_dcc $num
	} else {
	    fcopy $idx $dcc($num,fd) -command [list [namespace current]::dcc_client_in $num $idx] -size 1024
	}
    }
}
