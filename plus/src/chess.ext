EXTENTION chess-0.9-7
# $Id: chess.ext,v 1.9 2001-10-05 23:37:57 urish Exp $

# File structure:
# * Variable initialization
# * Extention routines (init/destroy)
# * Interface and canvas initialization routines
# * Square/Piece coordinates routines
# * Piece graphic routines (draw piece, piece blink)
# * Basic piece routines (create piece, move piece, get piece type, ...)
# * Advanced piece routines (find next piece on line, ...)
# * Board drawing routines
# * Game routines (init/destory/winning a game)
# * Move-Legallity checking Routines (is king threatened, can move, ...)
# * Move performing routine (move piece)
# * Piece event routines (piece click, piece drag, etc.)

namespace eval chess {
    variable colors
    variable pieces
# chess piece definition
    array set pieces {rook,coords {32 44} king {0 8 8 0 16 0 24 7 24 0 32 0 32 7 40 0 48 0 56 8 56 24 41 39 15 39 0 24 0 8 8 12 13 7 15 7 24 15 24 32 16 32 8 24 8 12 0 8 32 16 41 7 43 7 48 12 48 24 40 32 32 32 32 16 0 8} rook {0 0 6 0 6 7 13 7 13 0 19 0 19 7 26 7 26 0 32 0 32 13 26 19 26 34 32 40 32 44 0 44 0 38 6 32 6 18 0 13 0 0} bishop,coords {32 49} knight,coords {36 47} pawn,coords {24 37} queen,coords {55 44} bishop {0 49 32 49 32 44 19 37 19 30 22 30 22 24 19 24 19 21 23 19 24 18 25 18 20 18 25 12 25 11 16 0 7 11 6 11 6 18 9 21 12 21 12 22 12 24 9 24 10 30 13 30 13 37 0 44 0 49} knight {36 47 36 32 33 16 32 16 15 0 15 9 0 29 0 30 5 35 7 35 18 25 19 25 19 26 18 27 12 47 36 47} pawn {0 37 24 37 24 33 23 33 14 24 14 18 19 18 19 14 15 14 15 13 19 9 19 4 18 4 14 0 10 0 5 4 5 9 9 13 9 14 5 14 5 18 10 18 10 25 8 25 0 33 0 37} queen {8 44 48 44 48 36 55 6 42 20 37 0 28 19 19 0 13 20 0 6 8 36 8 44} king,coords {56 39}}
    set colors(player0) orange
    set colors(player1) red
    set colors(square0) white
    set colors(square1) black

    proc init {} {
	onevent usercmd CHESS [namespace current]::ucmd_chess
	iface_init
	canvas_init
    }
    
    proc destroy {} {
	unevent usercmd [namespace current]::ucmd_chess
	destroy_widget .chess
	namespace delete [namespace current]
    }

    proc iface_init {} {
	toplevel .chess
	wm title .chess "XCchess"
	wm protocol .chess WM_DELETE_WINDOW [namespace current]::window_close
	wm withdraw .chess
    }

    proc canvas_init {} {
	canvas .chess.canvas -relief raised -border 2 -height [expr 64 * 8 + 28] -width [expr 64 * 8 + 28] -background gray
	grid configure .chess.canvas -row 1 -column 1
	draw_board 64 64
    }
    
    proc get_square_by_coords {x y} {
	variable board
	return [list \
	    [expr ($x - $board(hoffs)) / $board(hsize)] \
	    [expr ($y - $board(voffs)) / $board(vsize)]]
    }

    proc get_square_coords {h v} {
	variable board
	return [list	\
	    [expr $board(hoffs) + $h * $board(hsize)]	\
	    [expr $board(voffs) + $v * $board(vsize)]	\
	    [expr $board(hoffs) + $h * $board(hsize) + $board(hsize) - 1]\
	    [expr $board(voffs) + $v * $board(vsize) + $board(vsize) - 1]]
    }
    
    proc get_piece_coords {h v} {
	set coords [get_square_coords $h $v]
	return [list \
	    [expr [lindex $coords 0] + 2] \
	    [expr [lindex $coords 1] + 2] \
	    [expr [lindex $coords 2] - 2] \
	    [expr [lindex $coords 3] - 2]]
    }
    
    proc format_coords {coords} {
	set letters "ABCDEFGH"
	return [string index $letters [lindex $coords 0]][expr [lindex $coords 1] + 1]
    }
    
    proc draw_piece {h v piece color} {
	variable pieces
	variable colors
	set ucoords [get_piece_coords $h $v]
	set centerx [expr ([lindex $ucoords 0] + [lindex $ucoords 2]) / 2]
	set basex [expr $centerx - [lindex $pieces($piece,coords) 0] / 2]
	set basey [expr [lindex $ucoords 3] - [lindex $pieces($piece,coords) 1]]
	foreach {x y} $pieces($piece) {
	    lappend polycoords [expr int($x) + $basex] \
			       [expr int($y) + $basey]
	}
	set piece [eval .chess.canvas create polygon $polycoords -fill [list $color]]
	.chess.canvas addtag pieces withtag $piece
	return $piece
    }

    proc piece_blink {piece} {
	variable game
	set blinkstates "gray75 gray50 gray25 gray50 gray75"
	if {[incr game(blinkstate)] > [llength $blinkstates]} {
	    set game(blinkstate) 0
	} 
	.chess.canvas itemconfigure $piece -stipple [lindex $blinkstates $game(blinkstate)]
	set game(blinktimer) [after 100 "[namespace current]::piece_blink $piece"]
    }
    
    proc stop_piece_blink {piece} {
	variable game
	if ![info exists game(blinktimer)] {
	    return
	}
	after cancel $game(blinktimer)
	unset game(blinktimer)
	.chess.canvas itemconfigure $piece -stipple ""
    }
    
    proc create_piece {h v player type} {
	variable board
	variable colors
	set piece [draw_piece $h $v $type $colors(player$player)]
	set board(square$h$v) $piece
	set board(piece$piece) [list $player $type]
	if {$type == "king"} {
	    set board(king$player) [list $h $v]
	}
	return $piece
    }
    
    proc move_piece_on_board {piece source target} {
	set origxy [eval get_piece_coords $source]
	set targxy [eval get_piece_coords $target]
	.chess.canvas move $piece [expr [lindex $targxy 0] - [lindex $origxy 0]] \
				  [expr [lindex $targxy 1] - [lindex $origxy 1]]
    }

    proc is_piece_at_coords {coords} {
	variable board
	return [info exists board(square[join $coords ""])]
    }
    
    proc get_piece_by_coords {coords} {
	variable board
	set square square[join $coords ""]
	if [info exists board($square)] {
	    return $board($square)
	} else {
	    return ""
	}
    }
    
    proc get_piece_player_by_coords {coords} {
	variable board
	set square square[join $coords ""]
	if [info exists board($square)] {
	    return [lindex $board(piece$board($square)) 0]
	} else {
	    return ""
	}
    }

    proc get_piece_type_by_coords {coords} {
	variable board
	set square square[join $coords ""]
	if [info exists board($square)] {
	    return [lindex $board(piece$board($sqaure)) 1]
	} else {
	    return ""
	}
    }
    
    proc get_piece_player {piece} {
	variable board
	return [lindex $board(piece$piece) 0]
    }

    proc get_piece_type {piece} {
	variable board
	return [lindex $board(piece$piece) 1]
    }
    
    proc get_piece_info {h v} {
	variable board
	set square square$h$v
	if [info exists board($square)] {
	    return $board(piece$board($square))
	} else {
	    return ""
	}
    }
    
    proc piece_match {h v player type} {
	variable board
	set square square$h$v
	if ![info exists board($square)] {
	    return 0
	}
	set piece $board(piece$board($square))
	return [expr [lindex $piece 0] == $player && [lsearch $type [lindex $piece 1]] != -1]
    }
    
    proc destroy_piece {coords} {
	variable board
	set piece $board(square[join $coords ""])
	unset board(square[join $coords ""])
	unset board(piece$piece)
	if {[.chess.canvas find withtag current] == $piece} {
	    stop_piece_blink $piece
	}
	.chess.canvas delete $piece
    }

    proc destroy_captured_piece {player} {
	variable board
	variable game
	if ![info exists game(captured_piece)] {
	    return
	}
	set piece $game(captured_piece)
	unset board(piece$piece)
	unset game(captured_piece)
	.chess.canvas delete $piece
	incr game(player$player) -1
    }
    
    proc piece_move {piece source target} {
	variable board
	variable game
	if [info exists board(square[join $target ""])] {
	    set game(captured_piece) $board(square[join $target ""])
	}
	if {[get_piece_type $piece] == "king"} {
	    set board(king[get_piece_player $piece]) $target
	}
	unset board(square[join $source ""])
	set board(square[join $target ""]) $piece
    }
    
    proc piece_unmove {piece source target} {
	variable board
	variable game
	if [info exists game(captured_piece)] {
	    set board(square[join $target ""]) $game(captured_piece)
	    unset game(captured_piece)
	} else {
	    unset board(square[join $target ""])
	}
	if {[get_piece_type $piece] == "king"} {
	    set board(king[get_piece_player $piece]) $source
	}
	set board(square[join $source ""]) $piece
    }

    proc get_king_coords {player} {
	variable board
	return $board(king$player)
    }

    proc promote_piece {coords newtype player} {
	destroy_piece $coords
	eval create_piece $coords $player $newtype
    }
    
    proc find_next_piece_on_line {h v isxdir dir} {
	for {set i [expr ($isxdir ? $h : $v) + $dir]} {$i < 8 && $i >= 0} \
	    {incr i $dir} {
	    if {$isxdir} {
		set pinfo [get_piece_info $i $v]
	    } else {
		set pinfo [get_piece_info $h $i]
	    }
	    if {$pinfo != ""} {
		return $pinfo
	    }
	}
	return ""
    }
    
    proc find_next_piece_on_diagonal {h v xdir ydir} {
	for {incr h $xdir; incr v $ydir} {$h < 8 && $h >= 0 && $v < 8 && $v >= 0} \
	    {incr h $xdir; incr v $ydir} {
	    set pinfo [get_piece_info $h $v]
	    if {$pinfo != ""} {
		return $pinfo
	    }
	}
	return ""
    }
    
    proc board_draw {args} {
	.chess.canvas addtag board withtag [uplevel ".chess.canvas create $args"]
    }
    
    proc draw_board {{hsize 0} {vsize 0} {hoffs 0} {voffs 0}} {
	variable board
	variable colors
	if !$hsize {
	    set hsize [expr ([.drawing_canvas.canvas cget -height] - 28) / 8]
	    set hoffs [expr ([.drawing_canvas.canvas cget -height] - 28) % 8 / 2]
	}
	if !$vsize {
	    set vsize [expr ([.drawing_canvas.canvas cget -width] - 28) / 8]
	    set voffs [expr ([.drawing_canvas.canvas cget -width] - 28) % 8 / 2]
	}
	incr hoffs 15
	incr voffs 15
	set board(hsize) $hsize
	set board(vsize) $vsize
	set board(hoffs) $hoffs
	set board(voffs) $voffs
	set color 1
	.chess.canvas delete board
	board_draw rectangle [expr $hoffs - 12] [expr $voffs - 12] \
	 	[expr $hoffs + $hsize * 8 + 14] [expr $voffs + $vsize * 8 + 14] \
		-fill green -outline black -stipple gray25
	board_draw rectangle [expr $hoffs - 1] [expr $voffs - 1] \
	 	[expr $hoffs + $hsize * 8] [expr $voffs + $vsize * 8] \
		-fill "" -outline black
	set letters "ABCDEFGH"
	for {set i 1} {$i <= 8} {incr i 2} {
	    board_draw text [expr $hoffs - 7] \
		[expr int((8 - $i + 0.5) * $vsize) + $voffs] -text "$i"
	    incr i -1
	    board_draw text [expr int(($i + 0.5) * $hsize) + $hoffs] \
		 [expr $voffs + 8 * $vsize + 7] -text [string index $letters $i]
	}
	for {set h 0} {$h < 8} {incr h} {
	    for {set v 0} {$v < 8} {incr v} {
		eval board_draw rectangle [get_square_coords $h $v] {-fill $colors(square$color) -outline $colors(square$color)}
		set color [expr !$color]
	    }
	    set color [expr !$color]
	}
    }
    
    proc init_game {} {
	variable board
	variable colors
	variable game
	wm deiconify .chess
	set game(active_player) 0
	set game(castling0s) 1
	set game(castling0l) 1
	set game(castling1s) 1
	set game(castling1l) 1
	set game(enpassent) ""
	set h 0
	set v 0
	set game(player0) 16
	set game(player1) 16
	set game(movelist) ""
	set game(enpassant_square) ""
	foreach type {rook knight bishop king queen bishop knight rook
		      pawn pawn pawn pawn pawn pawn pawn pawn} {
	    create_piece $h $v 1 $type
	    create_piece $h [expr 7 - $v] 0 $type
	    incr h
	    if {$h == 8} {
		set h 0
		incr v
	    }
	}
	.chess.canvas bind pieces <Any-Enter> "[namespace current]::piece_enter"
	.chess.canvas bind pieces <Any-Leave> "[namespace current]::piece_leave"
	.chess.canvas bind pieces <ButtonPress-1> "[namespace current]::piece_press %x %y"
	.chess.canvas bind pieces <ButtonRelease-1> "[namespace current]::piece_release %x %y"
	.chess.canvas bind pieces <B1-Motion> "[namespace current]::piece_drag %x %y"
    }
    
    proc finish_game {} {
	variable board
	variable game
	.chess.canvas delete pieces
	foreach i [concat [array names board sq*] [array names board qu*] \
		  [array names board u*]] {
	    unset board($i)
	}
	foreach i [concat [array names game p*] network isblack] {
	    unset game($i)
	}
	wm withdraw .chess
    }
    
    proc game_win {player} {
	variable game
	if $game(network) {
	    if $player {
		putcmsg xcplus_chess_youlost p $game(peer)
		tk_dialog .chess.dialog "You lost !" "You lost the game :-(" info 0 "Bummer !"
	    } else {
		putcmsg xcplus_chess_youwon p $game(peer)
		tk_dialog .chess.dialog "You won !" "You won the game !" info 0 "Yey !"
	    }
	} else {
	    set name [expr {$player ? "black" : "white"}]
	    putcmsg xcplus_chess_win t $name
	    if [tk_dialog .chess.dialog "The $name won !" "The $name player won the game !" info 1 "Dismiss" "Another Game !"] {
		set new_game 1
	    }
	}
	finish_game
	if [info exists new_game] {
	    set game(network) 0
	    set game(isblack) 0
	    init_game
	}
    }
    
    proc is_king_threatened {player {h -1} {v -1}} {
	set oplayer [expr !$player]
	if {$h == -1 || $v == -1} {
	    set kingcoords [get_king_coords $player]
	    set h [lindex $kingcoords 0]
	    set v [lindex $kingcoords 1]
	}
	
	# check for a pawn
	foreach x {1 -1} {
	    set y [expr $oplayer ? -1 : 1]
	    set piece [get_piece_info [expr $h + $x] [expr $v + $y]]
	    if {[lindex $piece 0] == $oplayer && [lindex $piece 1] == "pawn"} {
		return 1
	    }
	}
	
	# check for a bishop/queen/rook
	foreach x {1 1 -1 -1} \
		y {1 -1 1 -1} {
    
            # check for a bishop/queen
	    set piece [find_next_piece_on_diagonal $h $v $x $y]
	    if {[lindex $piece 0] == $oplayer && 
		[lsearch "bishop queen" [lindex $piece 1]] != -1} {
		return 1
	    }
    
            # check for a rook/queen
	    incr x
	    set piece [find_next_piece_on_line $h $v $x $y]
	    if {[lindex $piece 0] == $oplayer && 
		[lsearch "rook queen" [lindex $piece 1]] != -1} {
		return 1
	    }
	}

	# check for knight
	foreach x {-1 -1 1 1 -2 -2 2 2} \
		y {-2 2 -2 2 -1 1 -1 1} {
	    if [piece_match [expr $h + $x] [expr $v + $y] $oplayer knight] {
		return 1
	    }
	}
	
	# check for a foreign king
	foreach x {-1 0 1} {
	    foreach y {-1 0 1} {
		if [piece_match [expr $h + $x] [expr $v + $y] $oplayer king] {
		    return 1
		}
	    }
	}
	return 0
    }
    
    proc can_pawn_move {sx sy tx ty player} {
	variable game
	set direction [expr $player ? 1 : -1]
	set xmoveok 0
	if {$tx == $sx && ![is_piece_at_coords [list $tx $ty]]} {
	    set xmoveok 1
	} elseif {abs($tx - $sx) == 1} {
	    if {[get_piece_player_by_coords [list $tx $ty]] == !$player} {
	        set xmoveok 1
	    } elseif {$game(enpassant_square) == [list $tx $ty]} {
		set epdestroy [list $tx [expr $ty - $direction]]
		set xmoveok 1
	    }
	}
	if {$ty == $sy + $direction} {
	    if [info exists epdestroy] {
		set game(enpassant_destroy) $epdestroy
	    }
	    return $xmoveok
	}
	if {$player ? $sy == 1 && $ty == 3 : $sy == 6 && $ty == 4} {
	    set game(enpassant_square) [list $tx [expr $player ? 2 : 5]]
	    return $xmoveok
	}
	return 0
    }
    
    proc can_move_linear {sx sy tx ty} {
	if {$tx == $sx} {
	    set start [expr ($ty > $sy ? $sy : $ty) + 1]
	    set end [expr $ty > $sy ? $ty : $sy]
	    for {set y $start} {$y < $end} {incr y} {
		if [is_piece_at_coords [list $sx $y]] {
		    return 0
		}
	    }
	} elseif {$ty == $sy} {
	    set start [expr ($tx > $sx ? $sx : $tx) + 1]
	    set end [expr $tx > $sx ? $tx : $sx]
	    for {set x $start} {$x < $end} {incr x} {
		if [is_piece_at_coords [list $x $sy]] {
		    return 0
		}
	    }
	} else {
	    return 0
	}
	return 1
    }
    
    proc can_move_diagonal {sx sy tx ty} {
	if {abs($tx - $sx) != abs($ty - $sy)} {
	    return 0
	}
	set xdir [expr $tx > $sx ? 1 : -1]
	set ydir [expr $ty > $sy ? 1 : -1]
	for {incr sx $xdir; incr sy $ydir} {$sx != $tx} {incr sx $xdir; incr sy $ydir} {
	    if [is_piece_at_coords [list $sx $sy]] {
		return 0
	    }
	}
	return 1
    }

    proc can_king_castle {source target player} {
	variable game
	if {!$game(castling${player}s) && !$game(castling${player}l)} {
	    return 0
	}
        if {[is_king_threatened $player]} {
	    return 0
	}
	set xdir [expr [lindex $target 0] > [lindex $source 0] ? 1 : -1]
	set tx [lindex $target 0]
	set y [lindex $target 1]
	for {set x [expr [lindex $source 0] + $xdir]} {$xdir + 1 ? $x <= $tx : $x >= $tx} \
	    {incr x $xdir} {
	    if {[get_piece_info $x $y] != ""} {
		return 0
	    }
	    if [is_king_threatened $player $x $y] {
		return 0
	    }
	}
	if {$target == ($player ? "1 0" : "1 7") && $game(castling${player}s)} {
	    if {$player} {
		set game(castling_rook_move) "0 0 2 0"
	    } else {
		set game(castling_rook_move) "0 7 2 7"
	    }
	    return 1
	}
	if {$target == ($player ? "5 0" : "5 7") && $game(castling${player}l)} {
	    if {$player} {
		set game(castling_rook_move) "7 0 4 0"
	    } else {
		set game(castling_rook_move) "7 7 4 7"
	    }
	    return 1
	}
	return 0
    }
    
    proc can_move {piece type source target player} {
	if {$source == $target} {
	    return 0
	}
	if {[get_piece_player_by_coords $target] == $player} {
	    return 0
	}
	set sx [lindex $source 0]
	set sy [lindex $source 1]
	set tx [lindex $target 0]
	set ty [lindex $target 1]
	if {$tx < 0 || $tx > 7 || $ty < 0 || $ty > 7} {
	    return 0
	}
	switch $type {
	    pawn {
		return [can_pawn_move $sx $sy $tx $ty $player]
	    }
	    knight {
		return [expr abs($tx - $sx) + abs($ty - $sy) == 3 && $tx != $sx && $ty != $sy]
	    }
	    bishop {
		return [can_move_diagonal $sx $sy $tx $ty]
	    }
	    rook {
		return [can_move_linear $sx $sy $tx $ty]
	    }
	    queen {
		return [expr [can_move_linear $sx $sy $tx $ty] || \
			     [can_move_diagonal $sx $sy $tx $ty]]
	    }
	    king {
		if {(abs($tx - $sx) < 2 && abs($ty - $sy < 2))} {
		    return 1
		}
		if [can_king_castle $source $target $player] {
		    return 1
		}
	    }
	}
	return 0
    }

    proc is_move_legal {piece type source target player} {
	if ![can_move $piece $type $source $target $player]} {
	    return 0
	}
	piece_move $piece $source $target
	set check [is_king_threatened $player]
	piece_unmove $piece $source $target
	return [expr !$check]
    }

    proc linear_move_available {piece h v type player} {
	set source [list $h $v]
	for {set i 0} {$i < 8} {incr i} {
	    foreach j {0 1} {
		if {$j} {
		    set target [list $i $v]
		} else {
		    set target [list $h $i]
		}
	    	if [is_move_legal $piece $type $source $target $player] {
		    return 1
	    	}
	    }
	}
	return 0
    }

    proc diagonal_move_available {piece h v type player} {
	set source [list $h $v]
	for {set i 1} {$i < 8} {incr i} {
	    foreach hi {1 1 -1 -1} \
	    	    vi {1 -1 1 -1} {
	    	set target [list [expr $h + $i * $hi] [expr $v + $i * $vi]]
		if [is_move_legal $piece $type $source $target $player] {
		    return 1
		}
	    }
	}
	return 0
    }
    
    proc moves_available {player} {
	variable board
	foreach i [array names board square*] {
	    if {[get_piece_player $board($i)] != $player} {
		continue
	    }
	    set h [string index $i 6]
	    set v [string index $i 7]
	    set type [get_piece_type $board($i)]
	    switch $type {
		rook {
		    if [linear_move_available $board($i) $h $v $type $player] {
			return 1
		    }
		}
		bishop {
		    if [diagonal_move_available $board($i) $h $v $type $player] {
			return 1
		    }
		}
		queen {
		    if {[diagonal_move_available $board($i) $h $v $type $player]
			|| [linear_move_available $board($i) $h $v $type $player]} {
			return 1
		    }
		}
		king - pawn {
		    set source [list $h $v]
		    foreach ho {-1 -1 -1 0  0  1 1 1}
		    	    vo {-1  0  1 -1 1 -1 0 1} {
			set target [list [expr $h + $ho] [expr $v + $vo]]
			if [is_move_legal $board($i) $type $source $target $player] {
			    return 1
			}
		    }
		}
		knight {
		    set source [list $h $v]
		    foreach ho {-2 -2 -1 -1 1  1 2  2} \
		    	    vo {1  -1  2 -2 2 -2 1 -1} {
			set target [list [expr $h + $ho] [expr $v + $vo]]
			if [is_move_legal $board($i) $type $source $target $player] {
			    return 1
			}
		    }
		}
	    }
	}
	return 0
    }
    
    proc move_piece {piece source target player} {
	variable board
	variable game
	set type [get_piece_type $piece]
	set epsquare $game(enpassant_square)
	if ![can_move $piece $type $source $target $player] {
	    return 0
	}
	piece_move $piece $source $target
	set check [is_king_threatened $player]
	if $check {
	    piece_unmove $piece $source $target
	    return 0
	}
	destroy_captured_piece [expr !$player]
	# enpassant capture could take place
	if {$epsquare != ""} {
	    set game(enpassant_square) ""
	    if [info exists game(enpassant_destroy)] {
		destroy_piece $game(enpassant_destroy)
		unset game(enpassant_destroy)
	    }
	}
	# check for promotion
	if {$type == "pawn" && [string match {[70]} [lindex $target 1]]} {
	    promote_piece $target queen $player
	}
	# disallow castling after king movement
	if {$type == "king"} {
	    set game(castling${player}s) 0
	    set game(castling${player}l) 0
	}
	# disallow castling after a rook movement
        if {$source == "0 0" || $source == "7 0"} {
	    set game(castling${player}s) 0
	} elseif {$source == "0 7" || $source == "7 7"} {
	    set game(castling${player}l) 0
	}
	# move the rook if castling occured
	if [info exists game(castling_rook_move)] {
	    set rsource [lrange $game(castling_rook_move) 0 1]
	    set rtarget [lrange $game(castling_rook_move) 2 3]
	    set rpiece [get_piece_by_coords $rsource]
	    piece_move $rpiece $rsource $rtarget
	    move_piece_on_board $rpiece $rsource $rtarget
	    unset game(castling_rook_move)
	}
	# chess for mate / stale mate
	if ![moves_available [expr !$player]] {
	    end_game $player [is_king_threatened [expr !$player]]
	}
	lappend game(movelist) "$source $target"
	set game(active_player) [expr !$player]
	return 1
    }
    
    proc piece_enter {} {
	variable game
	set game(blinkstate) 0
	piece_blink current
    }
    
    proc piece_leave {} {
	stop_piece_blink current
    }

    proc piece_press {x y} {
	variable current_piece
	variable board
	variable game
	set piece [.chess.canvas find withtag current]
	if {$game(active_player) != [get_piece_player $piece]} {
	    bell
	    return
	}
	.chess.canvas raise $piece
	set square [get_square_by_coords $x $y]
	set coords [eval get_square_coords $square]
	set current_piece(name) $piece
	set current_piece(lastx) $x
	set current_piece(lasty) $y
	set current_piece(xoffs) [expr $x - [lindex $coords 0]]
	set current_piece(yoffs) [expr $y - [lindex $coords 1]]
	set current_piece(square) $square
    }
    
    proc piece_drag {x y} {
	variable current_piece
	variable board
	if ![info exists current_piece] {
	    return
	}
	.chess.canvas move $current_piece(name) [expr $x - $current_piece(lastx)] [expr $y - $current_piece(lasty)]
	set current_piece(lastx) $x
	set current_piece(lasty) $y
    }

    proc piece_release {x y} {
	variable current_piece
	variable board
	variable game
	if ![info exists current_piece] {
	    return
	}
        set square [get_square_by_coords $x $y]
	set rc [move_piece $current_piece(name) $current_piece(square) $square $game(active_player)]
	if {$rc} {
	} else {
	    set square $current_piece(square)
	    bell
	}
	set coords [eval get_square_coords $square]
        set xmove [expr [lindex $coords 0] + $current_piece(xoffs) - $current_piece(lastx)]
        set ymove [expr [lindex $coords 1] + $current_piece(yoffs) - $current_piece(lasty)]
        eval .chess.canvas move $current_piece(name) $xmove $ymove
	unset current_piece
	return
	if [set ret ] {
	    eval .chess.canvas coords $current_piece(name) [eval get_piece_coords $square]
	    if $game(network) {
		putsock "CMD $game(peer) CKRS MOVE [format_coords $current_piece(coords)] [format_coords $square]"
	    }
	} else {
	    eval .chess.canvas coords $current_piece(name) [eval get_piece_coords $current_piece(coords)]
	}
	unset current_piece
    }

    proc window_close {} {
	variable game
	if [info exists game(peer)] {
	    putsock "CMD $game(peer) CKRS CANCEL"
	}
	finish_game
    }

    proc server_error {sargs} {
	variable game
	set errornum [strtok sargs]
	if {$errornum == 104} {
	    set nick [string tolower [strtok sargs]]
	    if [info exists game(network)] {
		if {$nick == $game(peer)} {
		    putcmsg xcplus_chess_disc p $game(peer)
		    tk_dialog .chess.dialog "Remote side disconnected." "The remote side disconnected." error 0 "Bummer !"
		    finish_game
		}
	    }
	    foreach i [array names game *,$nick] {
		unset game($i)
	    }
	}
	return 0
    }

    proc ucmd_chess {uargs} {
	variable game
	set peer [strtok uargs]
	set lpeer [string tolower $peer]
	if {[string toupper $peer] == "REFUSE"} {
	    set peer [strtok uargs]
	    set reason [strrest uargs]
	    if {$peer == ""} {
		return 1
	    }
	    if [info exists game(offers,$lpeer)] {
		unset game(offers,$lpeer)
		putcmsg xcplus_chess_refusing n $peer t $reason
		putsock "CMD $peer CKFS refuse $reason"
	    } else {
		putcmsg xcplus_chess_norefuse n $peer
	    }
	    return 1
	}
	if {[string toupper $peer] == "PLAY"} {
	    set peer [strtok uargs]
	}
	if [info exists game(network)] {
	    putcmsg xcplus_chess_inprogress
	    return 1
	}
	if {$peer == ""} {
	    set game(network) 0
	    set game(isblack) 0
	    init_game
	    return 1
	}
	if [info exists game(offers,$lpeer)] {
	    unset game(offers,$lpeer)
	    set game(network) 1
	    set game(isblack) 1
	    set game(peer) $lpeer
	    putcmsg xcplus_chess_start n $peer
	    init_game
	    putsock "CMD $peer CKRS START"
	    return 1
	}
        set game(offerto,$lpeer) 1
        putsock "CMD $peer CKRS PLAY"
	putcmsg xcplus_chess_offering n $peer
	return 1
    }
    
    proc server_cmd_ckrs {source cargs} {
	variable game
	set cargs [split $cargs]
	set cmd [strtok cargs]
	set lsource [string tolower $source]
	set multi 0
	if {[string toupper $cmd] == "MULTI"} {
	    set multi 1
	    set cmd [strtok cargs]
	}
	switch -exact -- [string toupper $cmd] {
	    PLAY {
		set game(offers,$lsource) 1
		putcmsg xcplus_chess_offer n $source
	    }
	    START {
		if [info exists game(network)] {
		    putsock "CMD $source REFUSE Game already in progress."
		}
		if [info exists game(offerto,$lsource)] {
		    unset game(offerto,$lsource)
		    set game(isblack) 0
		    set game(network) 1
		    set game(peer) $lsource
		    putcmsg xcplus_chess_start n $source
		    init_game
		} else {
		    putsock "CMD $source REFUSE I didn't offer you any game."
		}
	    }
	    CANCEL {
	        if [info exists game(network)] {
		    if {$lsource == $game(peer)} {
			putcmsg xcplus_chess_disc p $game(peer)
			tk_dialog .chess.dialog "Remote side disconnected." "The other player left the game." error 0 "Bummer !"
			finish_game
		    }
		}
	    }
	    REFUSE {
		set reason [strrest $cargs]
		putcmsg xcplus_chess_refuse n $source t $reason
	    }
	    MOVE {
		if ![info exists game(peer)] {
		    return 1
		}
		if {$game(peer) != $lsource} {
		    return 1
		}
		if !$game(pactive) {
		    putcmsg xcplus_chess_turncheat n $source
		    return 1
		}
		set letters "ABCDEFGH"
		set src [strtok cargs]
		set target [strtok cargs]
		if {[string length $src] != 2 || [string length $target] != 2} {
		    putcmsg xcplus_chess_badmove n $source
		    return 1
		}
		set num 0
		foreach {i j} [split $src$target ""] {
		    set i [string toupper $i]
		    if {![string match {[ABCDEFGH]} $i] || ![string match {[12345678]} $j]} {
			putcmsg xcplus_chess_badmove n $source
			return 1
		    }
		    set coords($num) [list [expr [string first $i $letters]] [expr $j - 1]]
		    incr num
		}
		if ![process_peer_move $coords(0) $coords(1) $multi] {
		    putcmsg xcplus_chess_cheatmove n $source
		}
	    }
	}
	return 1
    }    
}
